buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		maven { url 'http://mvn.revoltsoft.ru/' }
		maven { url 'https://plugins.gradle.org/m2/' }
	}
	dependencies {
		classpath 'com.eriwen:gradle-css-plugin:2.14.0'
		classpath 'com.eriwen:gradle-js-plugin:2.14.1'
	}
}

repositories {
	mavenLocal()
	maven { url 'http://mvn.revoltsoft.ru/' }
}

group = 'org.zenframework.z8'
version = '1.0'

ext.buildFolder = './target'
buildDir = buildFolder

ext.z8Version = '1.3.0'
if (System.properties['z8.home'] != null)
	ext.z8Home = file(System.properties['z8.home'])

ext.tmpPath = System.properties['java.io.tmpdir'] ?: '/tmp'

apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'war'
apply plugin: 'js'
apply plugin: 'css'
//apply plugin: 'maven'
//apply plugin: 'maven-publish'

configurations {
	compiler
	boot
	revolt { transitive = false }
	resources { transitive = false }
	blzip { transitive = false }
}

dependencies {
	implementation "org.zenframework.z8:org.zenframework.z8.server:${z8Version}"
	implementation "org.zenframework.z8:org.zenframework.z8.lang:${z8Version}"
	runtime "org.zenframework.z8:org.zenframework.z8.webserver:${z8Version}"
	runtime "org.zenframework.z8:org.zenframework.z8.oda.driver:${z8Version}"
	runtime "org.zenframework.z8.dependencies.jdbc:postgresql-42.0.0:3.0"

	boot "org.zenframework.z8:org.zenframework.z8.boot:${z8Version}"

	blzip !project.hasProperty('z8Home') ? "org.zenframework.z8:org.zenframework.z8.lang:${z8Version}:bl"
			: files("${z8Home}/org.zenframework.z8.lang/target/libs/org.zenframework.z8.lang-bl.jar")
	revolt !project.hasProperty('z8Home') ? "org.zenframework.z8:org.zenframework.z8.js:${z8Version}:rz"
			: files("${z8Home}/org.zenframework.z8.js/target/libs/org.zenframework.z8.js-rz.jar")
	resources !project.hasProperty('z8Home') ? "org.zenframework.z8:org.zenframework.z8.resources:${z8Version}:rz"
			: files("${z8Home}/org.zenframework.z8.resources/target/libs/org.zenframework.z8.resources-rz.jar")

	compiler "org.zenframework.z8:org.zenframework.z8.compiler:${z8Version}"
}

eclipse {
	// Z8 nature
	project.natures 'org.zenframework.z8.pde.ProjectNature'
	// Eclipse: default java output -> $buildDir/classes/default
	classpath.defaultOutputDir = new File(buildDir, 'classes/main')
	// Eclipse: java source folders output -> $buildFolder/classes/...
	classpath.file.whenMerged {
		entries.findAll { entry ->
			entry instanceof org.gradle.plugins.ide.eclipse.model.SourceFolder
		}.each { entry ->
			entry.output = entry.output.replace('bin/', "${buildFolder}/classes/")
		}
		if (!project.hasProperty('z8Home'))
			entries += new org.gradle.plugins.ide.eclipse.model.ProjectDependency('/org.zenframework.z8.lang')
	}
}

// Prepare resources

task defaultResources(type: Copy) {
	from zipTree(configurations.resources.singleFile)
	into "${projectDir}/src/main"
	eachFile {
		if (it.relativePath.getFile(destinationDir).exists()) {
			it.exclude()
		}
	}
}

// Bl & Java config

ext.blDepsPath = "${tmpPath}/${project.name}/dependencies/bl"
ext.blOutput = "${projectDir}/.java"

task unpackBlzip(type: Copy) {
	from zipTree(configurations.blzip.singleFile.absolutePath)
	into blDepsPath
}

task compileBl(type: JavaExec) {
	dependsOn unpackBlzip

	group = 'Build'
	description = 'Compile BL sources'

	classpath = configurations.compiler
	main = 'org.zenframework.z8.compiler.cmd.Main'
	args = [
		projectDir,
		"-projectName:${project.name}",
		"-output:${blOutput}",
		"-requires:${blDepsPath}"
	]
}

tasks.withType(JavaCompile) {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
	options.encoding = 'UTF-8'
}

sourceSets {
	main {
		java.srcDirs blOutput
		java.outputDir = file("${buildDir}/classes/main")
		resources.srcDirs blOutput
		output.resourcesDir = file("${buildDir}/classes/main")
	}
}

// Web config

combineCss {
	source = zipTree(configurations.revolt.singleFile).matching {
		include "web/debug/css/revolt.css"
	}.plus(file("$projectDir/src/main/css/.buildorder").readLines().findAll { !it.trim().isEmpty() }
			.collect { "${projectDir}/src/main/css/${it}" })
	dest = file("${buildDir}/web/debug/css/${project.name}.css")
}

minifyCss {
	source = combineCss
	dest = "${buildDir}/web/css/${project.name}.css"
	closure { warningLevel = 'QUIET' }
	doLast {
		ant.replaceregexp(match: '(calc\\([\\d|\\.]+[^+]*)(\\+)', replace:'\1 \2 ', flags:'g') {
			file "${buildDir}/web/css/${rootProject.name}.css"
		}
	}
}

combineJs {
	source = zipTree(configurations.revolt.singleFile).matching {
		include "web/debug/revolt.js"
	}.plus(file("$projectDir/src/main/js/.buildorder").readLines().findAll { !it.trim().isEmpty() }
			.collect { "${projectDir}/src/main/js/${it}" })
	dest = file("${buildDir}/web/debug/${project.name}.js")
	encoding = 'UTF-8'
}

minifyJs {
	source = combineJs
	dest = file("${buildDir}/web/${project.name}.js")
	closure {
		warningLevel = 'QUIET'
		compilerOptions = [
			languageIn: 'ECMASCRIPT6',
			languageOut: 'ECMASCRIPT5',
			rewritePolyfills: false
		]
	}
}

task prepareWeb(type: Copy) {
	dependsOn defaultResources
	from('src/main/web') {
		filesMatching('**/*.html') {
			expand project: project
		}
	}
	into "${buildDir}/web"
}

task prepareDebug(type: Copy) {
	dependsOn defaultResources
	from('src/main/web') {
		exclude 'WEB-INF/**'
		exclude 'debug.html'
		filesMatching('**/*.html') {
			expand project: project
		}
	}
	into "${buildDir}/web/debug"
}

task assembleWeb(group: 'Build', dependsOn: [ minifyCss, minifyJs, prepareWeb, prepareDebug ])

// Application config

application {
	mainClassName = 'org.zenframework.z8.server.engine.ServerMain'
}

ext.appJavaXmx = '2048M'
ext.appHost = java.net.InetAddress.getLocalHost().getHostAddress()
ext.appPort = 9080
ext.appMaxFormContentSize = 15000000

run {
	dependsOn assembleWeb
	jvmArgs = [
		"-Xmx${appJavaXmx}",
		"-Xbootclasspath/p:${configurations.boot.singleFile.toPath()}",
		"-Dorg.eclipse.jetty.server.Request.maxFormContentSize=${appMaxFormContentSize}",
		"-Dorg.mortbay.http.HttpRequest.maxFormContentSize=${appMaxFormContentSize}",
		"-Djava.rmi.server.hostname=${appHost}",
		"-Dz8.webserver.port=${appPort}",
		"-Dz8.webserver.webapp=${buildDir}/web"
	]
	args = ['-server', 'webserver']
	workingDir = "${buildDir}/web/WEB-INF"
}

// WAR config

war {
	dependsOn assembleWeb
	exclude('debug.html')
	from("${buildDir}/web") {
		include '**/*.css'
		include '**/*.js'
	}
	filesMatching('**/*.html') {
		expand project: project
	}

	// TODO Exclude WEB-INF/lib/*.pom files
	//exclude('**/*.pom')
}

// Distribution config

distributions {
	main {
		contents {
			from(buildDir) {
				include 'web/**'
				exclude 'web/debug/**'
			}
			from("$projectDir/src/main") {
				include 'bin/**'
				exclude 'bin/*.sh'
			}
			from("$projectDir/src/main") {
				include 'bin/*.sh'
				include 'conf/**'
				expand project: project
			}
		}
	}
}

distZip.dependsOn assembleWeb
distTar.dependsOn assembleWeb
